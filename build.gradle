plugins {
    id 'java'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.2'
}

group = 'wood.mike'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.grpc:grpc-protobuf:1.68.1'
    implementation 'io.grpc:grpc-stub:1.68.1'
    implementation 'com.google.protobuf:protobuf-java:3.25.5'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'wood.mike'
            artifactId = 'proto-definitions'
            version = '1.0.0'

            from components.java
        }
    }
    repositories {
        maven {
            name = "repsy"
            url = uri("https://repo.repsy.io/mvn/mikew01/default")
            credentials {
                username = System.getenv("REPSY_USERNAME")
                password = System.getenv("REPSY_PASSWORD")
            }
        }
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.25.5' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.68.1' }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}